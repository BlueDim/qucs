# To have CMake pick a Qt installation of your choice that won't be found
# automatically, set the CMAKE_PREFIX_PATH environment variable. For example:
# "export CMAKE_PREFIX_PATH=/usr/local/trolltech/qt4.3.5"
#
# You can change the install location by running cmake like this: mkdir build;
# cd build cmake .. -DCMAKE_INSTALL_PREFIX=/new/install/prefix
#
# By default, the prefix is "/usr/local"
#
# Tests are enabled by default. To run the tests, try one of these: make test
# env CTEST_OUTPUT_ON_FAILURE=1 make test    # for verbose output

project(qucs CXX)
cmake_minimum_required(VERSION 2.6)
cmake_policy(VERSION 2.6)

# use top VERSION file
file(STRINGS ${PROJECT_SOURCE_DIR}/../VERSION QUCS_VERSION)
message(STATUS "Configuring ${PROJECT_NAME} (GUI): VERSION ${QUCS_VERSION}")

set(PROJECT_VERSION "${QUCS_VERSION}")

set(PROJECT_VENDOR "Qucs team. This program is licensed under the GNU GPL")
set(PROJECT_COPYRIGHT_YEAR "2014")
set(PROJECT_DOMAIN_FIRST "qucs")
set(PROJECT_DOMAIN_SECOND "org")

#
set(CMAKE_BUILD_TYPE Debug)

# enable testing for current directory and below
enable_testing()

# If Git hash not defined, try to define it
if(NOT GIT)
  if(EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/../../.git)
    find_package(Git)
    # Get the latest abbreviated commit hash of the working branch
    execute_process(
      COMMAND ${GIT_EXECUTABLE} log --pretty=format:%h -n 1u
      WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
      OUTPUT_VARIABLE GIT_COMMIT_HASH)
    set(GIT ${GIT_COMMIT_HASH})
    message(STATUS "Found Git repository, last commit hash: ${GIT}")
  endif()
endif()

if(UNIX AND NOT APPLE)
  # string(TOLOWER ${PROJECT_NAME} PROJECT_NAME_LOWERCASE) set(BIN_INSTALL_DIR
  # "bin") set(DOC_INSTALL_DIR "share/doc/${PROJECT_NAME_LOWERCASE}/")
else()
  # set(BIN_INSTALL_DIR ".") set(DOC_INSTALL_DIR ".")
endif()

#
# Set position independed code PIC
#
IF (UNIX AND NOT APPLE)
  IF( CMAKE_SYSTEM_PROCESSOR STREQUAL "x86_64" )
    SET(CMAKE_POSITION_INDEPENDENT_CODE ON)
  ENDIF()
ENDIF()



include(CheckCXXCompilerFlag)
CHECK_CXX_COMPILER_FLAG("-std=c++11" COMPILER_SUPPORTS_CXX11)
CHECK_CXX_COMPILER_FLAG("-std=c++0x" COMPILER_SUPPORTS_CXX0X)

if(COMPILER_SUPPORTS_CXX11)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11 -fPIC")
elseif(COMPILER_SUPPORTS_CXX0X)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++0x")
else()
    message(STATUS "The compiler ${CMAKE_CXX_COMPILER} has no C++11 support. Please use a different C++ compiler.")
endif()

#
ADD_DEFINITIONS( -DHAVE_CONFIG_H )

SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall ") # enable warning level

IF(${USE_QT_VERSION} MATCHES "4")
    FIND_PACKAGE( Qt4 REQUIRED QtCore QtGui QtSvg QtXml QtScript QtTest)
    INCLUDE( ${QT_USE_FILE} )
    ADD_DEFINITIONS(${QT_DEFINITIONS} -DQT_DEPRECATED_WARNINGS)
ELSE()
    FIND_PACKAGE(Qt5 COMPONENTS Core Gui Widgets Svg Xml Script PrintSupport Test REQUIRED QUIET)
    INCLUDE_DIRECTORIES(
      ${Qt5Core_INCLUDE_DIRS}
      ${Qt5Widgets_INCLUDE_DIRS}
      ${Qt5Svg_INCLUDE_DIRS}
      ${Qt5Xml_INCLUDE_DIRS}
      ${Qt5Script_INCLUDE_DIRS}
      ${Qt5PrintSupport_INCLUDE_DIRS}
      ${Qt5Test_INCLUDE_DIRS} )

    # bug, the find package does not seem to set the QT_LIBRARIES, do it manually
    SET(QT_LIBRARIES  ${Qt5Core_LIBRARIES} ${Qt5Widgets_LIBRARIES} ${Qt5Svg_LIBRARIES} ${Qt5Script_LIBRARIES} ${Qt5PrintSupport_LIBRARIES} ${Qt5Test_LIBRARIES} )

    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${Qt5Widgets_EXECUTABLE_COMPILE_FLAGS}")
    ADD_DEFINITIONS(${QT_DEFINITIONS})
ENDIF()

IF(DISABLE_QDEBUG)
    message(STATUS "disable qDebug(): ${DISABLE_QDEBUG}")
    ADD_DEFINITIONS(-DQT_NO_DEBUG_OUTPUT -DQT_NO_DEBUG -DQT_USE_FAST_CONCATENATION -DQT_USE_FAST_OPERATOR_PLUS)
ENDIF()


#MESSAGE("QT_INCLUDES=[${QT_INCLUDES}]")
#MESSAGE("QT_LIBRARIES=[${QT_LIBRARIES}]")

# Check whether the Qt QTabWidget::setMovable() function exists.
# This function was added in Qt 4.5.
# * Maybe use CMake CheckCXXSourceCompiles in the future?
# TRY_COMPILE( HAVE_QTABWIDGET_SETMOVABLE
#         ${CMAKE_BINARY_DIR}
#         ${CMAKE_CURRENT_SOURCE_DIR}/../cmake/QTabWidget_setMovable.cpp
#         CMAKE_FLAGS "-DINCLUDE_DIRECTORIES:STRING=${QT_INCLUDES}"
#         LINK_LIBRARIES ${QT_QTCORE_LIBRARY} ${QT_QTGUI_LIBRARY}
#         OUTPUT_VARIABLE TRY_OUT )
# IF(NOT HAVE_QTABWIDGET_SETMOVABLE)
#   MESSAGE("QTabWidget::setMovable() not available, feature disabled.")
# ENDIF()

# configure the header config.h
configure_file("${PROJECT_SOURCE_DIR}/../config.h.cmake"
               "${PROJECT_BINARY_DIR}/config.h")

include_directories("${PROJECT_BINARY_DIR}")
include_directories(${CMAKE_SOURCE_DIR})

#
# Set position independed code PIC
#
if(UNIX AND NOT APPLE)
  if(CMAKE_SYSTEM_PROCESSOR STREQUAL "x86_64")
    set(CMAKE_POSITION_INDEPENDENT_CODE ON)
  endif()
endif()

include_directories(
  ${qucs_SOURCE_DIR}
  # ${qucs_SOURCE_DIR}/bitmaps ->no sources here
  ${qucs_SOURCE_DIR}/components
  ${qucs_SOURCE_DIR}/diagrams
  ${qucs_SOURCE_DIR}/dialogs
  # ${qucs_SOURCE_DIR}/octave ->no sources here
  ${qucs_SOURCE_DIR}/paintings)

# ADD_SUBDIRECTORY( bitmaps ) -> added as resources
add_subdirectory(components)
add_subdirectory(diagrams)
add_subdirectory(dialogs)
add_subdirectory(octave)
add_subdirectory(python)
add_subdirectory(paintings)

add_subdirectory(tests)

set(QUCS_SRCS
    attach.cpp
    frame.cpp
    graphicitem.cpp
    octave_window.cpp
    qucsdoc.cpp
    textdoc.cpp
    mnemo.cpp
    qucs.cpp
    module.cpp
    wire.cpp
    mouseactions.cpp
    mousecursor.cpp
    qucs_actions.cpp
    schematicfile.cpp
    schematicview.cpp
    schematicscene.cpp
    undocommands.cpp
    wirelabel.cpp
    node.cpp
    qucs_init.cpp
    syntax.cpp
    misc.cpp
    messagedock.cpp
    imagewriter.cpp
    printerwriter.cpp
    projectView.cpp)

set(QUCS_HDRS
    frame.h
    graphicitem.h
    messagedock.h
    misc.h
    mnemo.h
    module.h
    mouseactions.h
    mousecursor.h
    node.h
    undocommands.h
    octave_window.h
    platform.h
    qucs.h
    qucsdoc.h
    syntax.h
    textdoc.h
    wire.h
    wirelabel.h)

#
# files that have Q_OBJECT need to be MOC'ed
#
SET(QUCS_MOC_HDRS
  octave_window.h
  qucs.h
  schematicfile.h
  schematicview.h
  schematicscene.h
  textdoc.h
  messagedock.h
  projectView.h
)

#
# headers that need to be moc'ed
#
IF(${USE_QT_VERSION} MATCHES "4")
    #
    # headers that need to be moc'ed
    #
    QT4_WRAP_CPP( QUCS_MOC_SRCS ${QUCS_MOC_HDRS} )

    #
    # generate rules for building source files from bitmap resources
    #
    SET(RESOURCES qucs_.qrc)
    QT4_ADD_RESOURCES(RESOURCES_SRCS ${RESOURCES})
ELSE()
    #
    # headers that need to be moc'ed
    #
    QT5_WRAP_CPP( QUCS_MOC_SRCS ${QUCS_MOC_HDRS} )

    #
    # generate rules for building source files from bitmap resources
    #
    SET(RESOURCES qucs_.qrc)
    QT5_ADD_RESOURCES(RESOURCES_SRCS ${RESOURCES})
ENDIF()

if(UNIX AND NOT APPLE)

  set(ICON16 bitmaps/hicolor/16x16/apps/qucs.png)
  set(ICON22 bitmaps/hicolor/22x22/apps/qucs.png)
  set(ICON32 bitmaps/hicolor/32x32/apps/qucs.png)
  set(ICON48 bitmaps/hicolor/48x48/apps/qucs.png)
  set(ICON64 bitmaps/hicolor/64x64/apps/qucs.png)
  set(ICON128 bitmaps/hicolor/128x128/apps/qucs.png)
  set(ICON256 bitmaps/hicolor/256x256/apps/qucs.png)
  set(ICON512 bitmaps/hicolor/512x512/apps/qucs.png)
  set(ICONsc bitmaps/hicolor/scalable/apps/qucs.svg)

  install(FILES ${ICON16}
          DESTINATION ${CMAKE_INSTALL_PREFIX}/share/icons/hicolor/16x16/apps)
  install(FILES ${ICON22}
          DESTINATION ${CMAKE_INSTALL_PREFIX}/share/icons/hicolor/22x22/apps)
  install(FILES ${ICON32}
          DESTINATION ${CMAKE_INSTALL_PREFIX}/share/icons/hicolor/32x32/apps)
  install(FILES ${ICON48}
          DESTINATION ${CMAKE_INSTALL_PREFIX}/share/icons/hicolor/48x48/apps)
  install(FILES ${ICON64}
          DESTINATION ${CMAKE_INSTALL_PREFIX}/share/icons/hicolor/64x64/apps)
  install(FILES ${ICON128}
          DESTINATION ${CMAKE_INSTALL_PREFIX}/share/icons/hicolor/128x128/apps)
  install(FILES ${ICON256}
          DESTINATION ${CMAKE_INSTALL_PREFIX}/share/icons/hicolor/256x256/apps)
  install(FILES ${ICON512}
          DESTINATION ${CMAKE_INSTALL_PREFIX}/share/icons/hicolor/512x512/apps)
  install(FILES ${ICONsc}
          DESTINATION ${CMAKE_INSTALL_PREFIX}/share/icons/hicolor/scalable/apps)

  set(DESKTOP qucs.desktop)
  install(FILES ${DESKTOP}
          DESTINATION ${CMAKE_INSTALL_PREFIX}/share/applications)
endif()

#
# configure Apple bundle information
#
if(APPLE)
  # set information on Info.plist file
  set(MACOSX_BUNDLE_INFO_STRING "${PROJECT_NAME} ${PROJECT_VERSION}")
  set(MACOSX_BUNDLE_BUNDLE_VERSION "${PROJECT_NAME} ${PROJECT_VERSION}")
  set(MACOSX_BUNDLE_LONG_VERSION_STRING "${PROJECT_NAME} ${PROJECT_VERSION}")
  set(MACOSX_BUNDLE_SHORT_VERSION_STRING "${PROJECT_VERSION}")
  set(MACOSX_BUNDLE_COPYRIGHT "${PROJECT_COPYRIGHT_YEAR} ${PROJECT_VENDOR}")
  set(MACOSX_BUNDLE_GUI_IDENTIFIER
      "${PROJECT_DOMAIN_SECOND}.${PROJECT_DOMAIN_FIRST}")
  set(MACOSX_BUNDLE_BUNDLE_NAME "${PROJECT_NAME}")
  set(MACOSX_BUNDLE_ICON_FILE qucs.icns)

  # set where in the bundle to put the icns file
  set_source_files_properties(${CMAKE_CURRENT_SOURCE_DIR}/bitmaps/qucs.icns
                              PROPERTIES MACOSX_PACKAGE_LOCATION Resources)
  # include the icns file in the target
  set(QUCS_SRCS ${QUCS_SRCS} ${CMAKE_CURRENT_SOURCE_DIR}/bitmaps/qucs.icns)

  # This tells cmake where to place the translations inside the bundle
  # SET_SOURCE_FILES_PROPERTIES( ${LANG_SRCS} PROPERTIES MACOSX_PACKAGE_LOCATION
  # Resources/lang ) include the translation files in the target
  # SET(RESOURCES_SRCS ${RESOURCES_SRCS} ${LANG_SRCS})
endif(APPLE)

#
# Set up RPATH for the project
#
option(ENABLE_RPATH "Enable rpath support on Linux and Mac" ON)
if(NOT CMAKE_INSTALL_RPATH)
  set(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/lib")
endif()
if(APPLE AND NOT CMAKE_INSTALL_NAME_DIR)
  set(CMAKE_INSTALL_NAME_DIR "${CMAKE_INSTALL_PREFIX}/lib")
endif()
if(UNIX AND ENABLE_RPATH)
  set(CMAKE_SKIP_BUILD_RPATH FALSE)
  set(CMAKE_BUILD_WITH_INSTALL_RPATH FALSE)
  set(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)
  set(CMAKE_MACOSX_RPATH TRUE)
endif()

# schematic library
ADD_LIBRARY( coreSchematic OBJECT ${QUCS_SRCS} ${QUCS_HDRS} ${QUCS_MOC_SRCS})

# create shared library
add_library(
  qucsschematic SHARED
  $<TARGET_OBJECTS:coreSchematic> $<TARGET_OBJECTS:components>
  $<TARGET_OBJECTS:diagrams> $<TARGET_OBJECTS:dialogs>
  $<TARGET_OBJECTS:paintings>)

# link system libraries
target_link_libraries(qucsschematic ${QT_LIBRARIES} ${CMAKE_DL_LIBS})

#
# CMake's way of creating an executable
#
add_executable(qucs MACOSX_BUNDLE WIN32 main.cpp ${RESOURCES_SRCS})

#
# Tell CMake which libraries we need to link our shared library against.
#
TARGET_LINK_LIBRARIES( qucsschematic ${QT_LIBRARIES} ${CMAKE_DL_LIBS})

#
# Link executable to shared library
#
TARGET_LINK_LIBRARIES( qucs qucsschematic )

#
# Prepare the installation
#
set(plugin_dest_dir bin)
set(qtconf_dest_dir bin)
set(APPS "${CMAKE_INSTALL_PREFIX}/bin/${PROJECT_NAME}")
if(APPLE)
  set(plugin_dest_dir ${PROJECT_NAME}.app/Contents/MacOS)
  set(qtconf_dest_dir ${PROJECT_NAME}.app/Contents/Resources)
  set(APPS "${CMAKE_INSTALL_PREFIX}/bin/${PROJECT_NAME}.app")
endif(APPLE)

if(WIN32)
  set(APPS "${CMAKE_INSTALL_PREFIX}/bin/${PROJECT_NAME}.exe")
endif(WIN32)

#
# Install the Qucs application, on Apple, the bundle is installed as on other
# platforms it'll go into the bin directory.
#
install(
  TARGETS qucs
  BUNDLE DESTINATION bin COMPONENT Runtime
  RUNTIME DESTINATION bin COMPONENT Runtime)

# set Windows runtime location for libqucschematic See:
# http://www.cmake.org/pipermail/cmake/2010-June/037461.html
install(
  TARGETS qucsschematic
  RUNTIME DESTINATION bin COMPONENT runtime
  ARCHIVE DESTINATION lib COMPONENT devel
  LIBRARY DESTINATION lib COMPONENT library)

#
# Install needed Qt plugins by copying directories from the qt installation One
# can cull what gets copied by using 'REGEX "..." EXCLUDE'
#
if(APPLE)
  install(
    DIRECTORY "${QT_PLUGINS_DIR}/imageformats"
    DESTINATION bin/${plugin_dest_dir}/plugins
    COMPONENT Runtime)
endif()
#
# install a qt.conf file this inserts some cmake code into the install script to
# write the file
#
if(APPLE)
  install(CODE "
    file(WRITE \"\${CMAKE_INSTALL_PREFIX}/bin/${qtconf_dest_dir}/qt.conf\" \"\")
    " COMPONENT Runtime)
endif()

# ------------------------------------------------------------------------------
# --
# Use BundleUtilities to get all other dependencies for the application to work.
# It takes a bundle or executable along with possible plugins and inspects it
# for dependencies.  If they are not system dependencies, they are copied.

# directories to look for dependencies
if(APPLE)
  set(DIRS ${QT_LIBRARY_DIRS})
endif()

# Now the work of copying dependencies into the bundle/package The quotes are
# escaped and variables to use at install time have their $ escaped An
# alternative is the do a configure_file() on a script and use install(SCRIPT
# ...). Note that the image plugins depend on QtSvg and QtXml, and it got those
# copied over.
if(APPLE)
  install(
    CODE "
    file(GLOB_RECURSE QTPLUGINS
      \"\${CMAKE_INSTALL_PREFIX}/bin/${plugin_dest_dir}/plugins/*${CMAKE_SHARED_LIBRARY_SUFFIX}\")
    set(BU_CHMOD_BUNDLE_ITEMS ON)
    include(BundleUtilities)
    fixup_bundle(\"${APPS}\" \"\${QTPLUGINS}\" \"${DIRS}\")
    "
    COMPONENT Runtime)
endif()

# Install wrapper scripts
if(WIN32)
  set(SCRIPTS qucsdigi.bat qucsveri.bat qucsdigilib.bat)
else()
  set(SCRIPTS qucsdigi qucsveri qucsdigilib)
endif()
install(FILES ${SCRIPTS} DESTINATION bin/)

# To Create a package, one can run "cpack -G DragNDrop CPackConfig.cmake" on Mac
# OS X where CPackConfig.cmake is created by including CPack And then there's
# ways to customize this as well set(CPACK_BINARY_DRAGNDROP ON) include(CPack)

